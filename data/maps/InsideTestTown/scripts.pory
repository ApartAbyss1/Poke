mapscripts InsideTestTown_MapScripts {
}

script InsideTestTown_EventScript_MudkipPokeBall {
    lockall
    msgbox (InsideTestTown_Text_TakeThisBall, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        msgbox (InsideTestTown_Text_LeftTheBall)
        releaseall
        end
    }
    else {
        givemon (SPECIES_MUDKIP, 15, ITEM_MASTER_BALL)
        if (var(VAR_RESULT) == 0) {
            call (InsideTestTown_EventScript_MudkipFanFare)
            msgbox (gText_NicknameThisPokemon, MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                call (Common_EventScript_GetGiftMonPartySlot)
                call (Common_EventScript_NameReceivedPartyMon)
            }
            setflag (FLAG_UNUSED_0x020)
        }
        elif (var(VAR_RESULT) == 1) {
            call (InsideTestTown_EventScript_MudkipFanFare)
            msgbox (gText_NicknameThisPokemon, MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                call (Common_EventScript_NameReceivedBoxMon)
            }
            call (Common_EventScript_TransferredToPC)
            setflag (FLAG_UNUSED_0x020)
        }
        else {
            goto (Common_EventScript_NoMoreRoomForPokemon)
        }
        releaseall
        end
    }
}

script InsideTestTown_EventScript_MudkipFanFare {
    bufferspeciesname (1, SPECIES_MUDKIP)
    removeobject (1)
    playfanfare (MUS_CAUGHT)
    setflag (FLAG_SYS_POKEMON_GET)
    message (InsideTestTown_Text_ObtainedMudkip)
    waitmessage
    waitfanfare
    bufferspeciesname (0, SPECIES_MUDKIP)
    return
}

text InsideTestTown_Text_LeftTheBall {
    "Decdied to leave the Poke Ball."
}

text InsideTestTown_Text_TakeThisBall {
    "This Poke Ball contains a Mudkip. \p"
    "Will you take it?" 
}

text InsideTestTown_Text_ObtainedMudkip {
    "{PLAYER} obtained Mudkip!"
}